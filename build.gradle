plugins {
  id 'org.springframework.boot' version '2.6.6'
  id 'io.spring.dependency-management' version '1.0.11.RELEASE'
  id 'java'
  id 'com.diffplug.spotless' version '6.3.0'
  id 'com.github.spotbugs' version '4.6.0'
  id 'pmd'
}

group = 'dev.huka'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
  compileOnly {
    extendsFrom annotationProcessor
  }
}

repositories {
  mavenCentral()
}

dependencies {
  implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
  implementation 'info.picocli:picocli-spring-boot-starter:4.6.3'
  compileOnly 'org.projectlombok:lombok'
  annotationProcessor 'org.projectlombok:lombok'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
  testImplementation 'org.junit.jupiter:junit-jupiter-params'
  implementation 'com.fasterxml.jackson.core:jackson-databind'
  implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
  implementation 'org.thymeleaf.extras:thymeleaf-extras-java8time'
  implementation 'com.itextpdf:html2pdf:4.0.1'
}

pmd {
  consoleOutput = true
}

spotless {
  format 'misc', {
    target '*.gradle', '*.md', '.gitignore'
    trimTrailingWhitespace()
    indentWithSpaces(2)
    endWithNewline()
  }
  java {
    toggleOffOn('formatter:off', 'formatter:on')
    target 'src/*/java/**/*.java'
    importOrder('java', 'javax', 'org', 'com')
    removeUnusedImports()
    googleJavaFormat()
    licenseHeaderFile rootProject.file('spotless.license')
  }
}

tasks.matching { task -> task.name.startsWith('spotbugs') }.forEach {
  it.reports {
    html {
      enabled = true
      destination = file('$buildDir/spotbugs/main/spotbugs.html')
      stylesheet = 'fancy-hist.xsl'
    }
    xml.enabled = false
  }
}

tasks.named('test') {
  useJUnitPlatform()
}
